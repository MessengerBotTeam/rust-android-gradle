import groovy.json.JsonBuilder
import org.gradle.util.GradleVersion

plugins {
    id 'com.gradle.plugin-publish' version '2.0.0'
    id "org.gradle.test-retry" version "1.6.4"
}

apply plugin: "java-gradle-plugin"
apply plugin: "maven-publish"
apply plugin: "groovy"
apply plugin: "kotlin"

gradlePlugin {
    website = 'https://github.com/MessengerBotTeam/rust-android-gradle'
    vcsUrl = 'https://github.com/MessengerBotTeam/rust-android-gradle.git'

    plugins {
        rustAndroidGradlePlugin {
            id = 'org.mozilla.rust-android-gradle.rust-android'
            implementationClass = 'com.nishtahir.RustAndroidPlugin'
            displayName = 'Plugin for building Rust with Cargo in Android projects'
            description = 'A plugin that helps build Rust JNI libraries with Cargo for use in Android projects.'
            tags.set(['rust', 'cargo', 'android'])
        }
    }
}

group 'org.msgbot.rust-android-gradle'
version "$plugin_version"

def isCI = (System.getenv('CI') ?: 'false').toBoolean()

// Maps supported Android plugin versions to the versions of Gradle that support it
def supportedVersions = [
        // 요청하신 8.13.2 (가정된 최신 버전)
        "8.13.2": ["8.13.2"],

        // AGP 8.x 시리즈 (최신 Gradle 요구)
        "8.7.0" : ["8.9.0", "8.13.2"],
        "8.6.0" : ["8.7.0", "8.13.2"],
        "8.5.0" : ["8.7.0", "8.13.2"],
        "8.4.0" : ["8.6.0", "8.13.2"],
        "8.3.0" : ["8.4.0", "8.13.2"],
        "8.2.0" : ["8.2.0", "8.13.2"],
        "8.1.0" : ["8.1.0", "8.13.2"],
        "8.0.0" : ["8.0.0", "8.13.2"],

        // AGP 7.x 시리즈
        "7.4.0" : ["7.5.0", "8.13.2"],
        "7.3.0" : ["7.4.0", "8.13.2"],
        "7.2.0" : ["7.3.3", "8.13.2"],
        "7.1.0" : ["7.2.0", "8.13.2"],

        // 기존 데이터
        "7.0.0" : ["7.1.1"],
        "4.2.2" : ["6.8.3", "7.1.1"],
        "4.1.3" : ["6.5.1", "6.8.3"],
        "4.0.2" : ["6.1.1", "6.8.3"],
        "3.6.4" : ["5.6.4", "6.8.3"],
        "3.5.4" : ["5.4.1", "5.6.4", "6.8.3"],
        "3.1.2" : ["4.10.2"]
]

// A local repo we publish our library to for testing in order to workaround limitations
// in the TestKit plugin classpath.
def localRepo = layout.buildDirectory.dir("local-repo").get().asFile
publishing {
    repositories {
        maven {
            url = localRepo.toURI()
        }
    }
}

dependencies {
    implementation gradleApi()
    compileOnly "com.android.tools.build:gradle:${agp_version}"
    implementation "com.google.guava:guava:33.0.0-jre"

    testImplementation gradleTestKit()
    testImplementation "com.android.tools.build:gradle:${agp_version}"
    testImplementation platform("org.spockframework:spock-bom:2.3-groovy-3.0")
    testImplementation("org.spockframework:spock-core") { exclude group: 'org.codehaus.groovy' }
    testImplementation("org.spockframework:spock-junit4") { exclude group: 'org.codehaus.groovy' }
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "com.google.guava:guava:33.0.0-jre"
}

kotlin {
    jvmToolchain(17)
}

// Generate a json file that contains the matrix of Gradle and AGP versions to test against.
def generatedResources = layout.buildDirectory.dir("generated-resources/main").get()
tasks.register('generateVersions') {
    def outputFile = generatedResources.file("versions.json").asFile
    inputs.property "version", version
    inputs.property "supportedVersions", supportedVersions
    outputs.dir generatedResources.asFile.absolutePath
    doLast {
        outputFile.text = new JsonBuilder([
                version          : version,
                supportedVersions: supportedVersions
        ]).toPrettyString()
    }
}

sourceSets {
    main {
        output.dir(generatedResources, builtBy: tasks.named('generateVersions'))
    }
}

// This is used by github actions to split out jobs by Android version test task
def generatedBuildResources = layout.buildDirectory.dir("build-resources").get()
tasks.register('generateTestTasksJson') {
    def outputFile = generatedBuildResources.file("androidTestTasks.json").asFile
    inputs.property "supportedVersions", supportedVersions
    outputs.dir generatedBuildResources.asFile.absolutePath
    doLast {
        outputFile.text = new JsonBuilder(
                // Fails in CI with issues invoking Java 11.  The single test that
                // requires Java 11 succeeds.  To be investigated in the future.
                // ['test'] +
                (supportedVersions.keySet().collect { androidVersion -> androidTestTaskName(androidVersion) })
        ).toString()
    }
}

// Configuration common to all test tasks
tasks.withType(Test).configureEach {
    dependsOn publish
    systemProperty "local.repo", localRepo.toURI()
    useJUnitPlatform()
    retry {
        maxRetries = isCI ? 1 : 0
        maxFailures = 20
    }
}

// Generate a test task for each Android version and run the tests annotated with the MultiVersionTest category
supportedVersions.keySet().each { androidVersion ->
    def testTaskName = androidTestTaskName(androidVersion)
    def jdkVersion = jdkVersionFor(androidVersion)
    def versionSpecificTest = tasks.register(testTaskName, Test) {
        description = "Runs the multi-version tests for AGP ${androidVersion} (JDK version ${jdkVersion})"
        group = "verification"

        javaLauncher = javaToolchains.launcherFor {
            languageVersion = JavaLanguageVersion.of(jdkVersion)
        }

        systemProperty 'org.gradle.android.testVersion', androidVersion
    }

    tasks.named('check').configure {
        dependsOn versionSpecificTest
    }
}

static def androidTestTaskName(String androidVersion) {
    return "testAndroid${normalizeVersion(androidVersion)}"
}

static def normalizeVersion(String version) {
    return version.replaceAll('[.\\-]', '_')
}

static def jdkVersionFor(String version) {
    def isAGP800OrNewer = GradleVersion.version(version) >= GradleVersion.version("8.0.0")
    def isAGP700OrNewer = GradleVersion.version(version) >= GradleVersion.version("7.0.0")

    if (isAGP800OrNewer) {
        return "17"
    } else if (isAGP700OrNewer) {
        return "11"
    } else {
        return "8"
    }
}
